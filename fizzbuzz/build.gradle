apply plugin: "java"
apply plugin: "application"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // To get started with GhostWriter, you need to reference and use the compile-time dependency that contains the preprocessor and the standard API
    // The preprocessor instruments the use of the GhostWriter API into your application.
    compile "io.ghostwriter:ghostwriter-openjdk-v7:0.1.1"

    // In order to actually capture and use the events that GhostWriter triggers a runtime implementation needs to be provided.
    // The GhostWriter stub check the classpath for TracerProvider implementations. GhostWriter will use the first one it finds.
    // If no implementation was found on the classpath, the default no-op will be used.
    runtime "io.ghostwriter:ghostwriter-rt-tracer:0.1.1"

    // SLF4J dependencies for providing the output generated by GhostWriter tracer runtime
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'
    runtime "org.slf4j:slf4j-simple:1.7.13"
}

ext {
    // For readability purposes we define a variable that contains the preprocessor class definition.
    // The concrete class depends on the JDK version that is used to compile the application
    // In case of JDKv8 (Java 8), this needs to be changed to "io.ghostwriter.openjdk.v8.GhostWriterAnnotationProcessor"
    gwAnnotationProcessor = "io.ghostwriter.openjdk.v7.GhostWriterAnnotationProcessor"
    mainClass = "io.ghostwriter.sample.fizzbuzz.FizzBuzz"
}

// In order to apply GhostWriter to the code base, the correct compile time preprocessor needs to be applied to the compilation step.
// This is done by providing the "-processor" argument to the Java compiler, that refers to the GhostWriter preprocessor class.
compileJava {
    options.compilerArgs = [
            // use the GhostWriter preprocessor to compile Java classes
            "-processor", gwAnnotationProcessor
    ]
}

jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}

// Trace level setting required in order to see the output from GhostWriter
applicationDefaultJvmArgs = ["-Dorg.slf4j.simpleLogger.defaultLogLevel=trace"]
mainClassName = mainClass